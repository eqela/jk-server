
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2021 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.fs
import jk.env
import jk.http.server
import jk.worker

class is CommonServer:

class MyHTTPServer is HTTPServer
{
	property myParent as parent
	{
	}

	func onMaintenance override
	{
		base.onMaintenance()
		myParent.onMaintenance()
	}
}

pvar handler as HTTPServerRequestHandler
pvar worker as typeinfo

var enableRequestLogging as bool
var enableRateLimit as string
var timeoutDelay as int
var maintenanceTimerDelay as int
var maxWorkerCount as int
var maxWorkerQueue as int

ctor
{
	initConfigValue("ENABLE_REQUEST_LOGGING", "true")
	initConfigValue("ENABLE_RATE_LIMIT", null)
	initConfigValue("TIMEOUT_DELAY", "30")
	initConfigValue("MAINTENANCE_TIMER_DELAY", "0")
	initConfigValue("MAX_WORKER_COUNT", "256")
	initConfigValue("MAX_WORKER_QUEUE", "1024")
}

func configure(key as string, value as string, relativeTo as File, error as Error) override as bool
{
	if key == "ENABLE_REQUEST_LOGGING" || key == "enableRequestLogging" {
		enableRequestLogging = Boolean.asBoolean(value)
		return true
	}
	if key == "ENABLE_RATE_LIMIT" || key == "enableRateLimit" {
		enableRateLimit = value
		return true
	}
	if key == "TIMEOUT_DELAY" || key == "timeoutDelay" {
		timeoutDelay = Integer.asValue(value, 30)
		return true
	}
	if key == "MAINTENANCE_TIMER_DELAY" || key == "maintenanceTimerDelay" {
		maintenanceTimerDelay = Integer.asValue(value, 0)
		return true
	}
	if key == "MAX_WORKER_COUNT" || key == "maxWorkerCount" {
		maxWorkerCount = Integer.asValue(value, 256)
		return true
	}
	if key == "MAX_WORKER_QUEUE" || key == "maxWorkerQueue" {
		maxWorkerQueue = Integer.asValue(value, 1024)
		return true
	}
	return base.configure(key, value, relativeTo, error)
}

func initializeHandlers(server as HTTPServer) virtual
{
	if handler:
		server.pushRequestHandler(handler)
	if worker:
		server.pushRequestHandler(HTTPServerRequestHandlerForWorker.forWorker(worker))
}

func initializeServer(server as HTTPServer) virtual as bool
{
	var workers = WorkerPool.forContext(ctx)
	if workers {
		workers.setMaxWorkers(maxWorkerCount)
		workers.setMaxQueue(maxWorkerQueue)
		server.workerPool = workers
	}
	if String.isNotEmpty(enableRateLimit) {
		var limit = new HTTPServerRateLimitHandler()
		if enableRateLimit == "true" || enableRateLimit == "yes" {
			nop // all good, use default settings
		}
		else {
			var comps = String.split(enableRateLimit, ':', 3)
			limit.setCountLimit(Integer.asInteger(Vector.get(comps, 0)))
			limit.setCountDuration(Integer.asInteger(Vector.get(comps, 1)))
			limit.setIgnoreDuration(Integer.asInteger(Vector.get(comps, 2)))
		}
		server.pushRequestHandler(limit)
	}
	initializeHandlers(server)
	return true
}

func initializeNetworkServer(server as NetworkServer) override as bool
{
	assert base.initializeNetworkServer(server)
	if server is HTTPServer {
		assert initializeServer(server as! HTTPServer)
		if enableRequestLogging:
			(server as! HTTPServer).addRequestHandlerListener(new HTTPServerRequestLogger().setLogContext(ctx).setLogdir(getLoggingDirectory()))
	}
	return true
}

func createServer override as NetworkServer
{
	var server = new MyHTTPServer()
	server.port = getListenPort()
	server.timeoutDelay = timeoutDelay
	server.maintenanceTimerDelay = maintenanceTimerDelay
	server.myParent = this
	return server
}

func executeForHandler(handler as HTTPServerRequestHandler, args as array<string>) as int
{
	setHandler(handler)
	return executeMain(args)
}
