
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2021 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.io
import jk.fs
import jk.log
import jk.console
import jk.server.web
import jk.http.server
import jk.http.server.fs

class is WebServer:

var contentDirectory as File
var listDirectories = true
var processTemplateFiles = false
var maxAge = 300

func configure(key as string, value as string, relativeTo as File, error as Error) override as bool
{
	if base.configure(key, value, relativeTo, error):
		return true
	if key == "maxAge" {
		maxAge = Integer.asInteger(value)
		return true
	}
	if key == "contentDirectory" {
		contentDirectory = File.forRelativePath(value, relativeTo)
		return true
	}
	if key == "listDirectories" {
		listDirectories = Boolean.asBoolean(value)
		return true
	}
	if key == "processTemplateFiles" {
		processTemplateFiles = Boolean.asBoolean(value)
		return true
	}
	return false
}

func acceptCommandLineArgument(arg as CommandLineArgument) override as bool
{
	if base.acceptCommandLineArgument(arg):
		return true
	if arg.isParameter() {
		contentDirectory = File.forPath(arg.parameter)
		return true
	}
	return false
}

func initializeServer(server as HTTPServer) override as bool
{
	assert base.initializeServer(server)
	assert contentDirectory:
		Log.error(ctx, "No `contentDirectory' specified. Either use a config file or specify -OcontentDirectory=<directory>")
	assert contentDirectory.isDirectory():
		Log.error(ctx, "Not a directory: `" .. contentDirectory.getPath() .. "'")
	var files = HTTPServerDirectoryHandler.forDirectory(contentDirectory)
	files.setMaxAge(maxAge)
	files.setListDirectories(listDirectories)
	files.setProcessTemplateFiles(processTemplateFiles)
	if processTemplateFiles {
		files.setIndexFiles([
			"index.html",
			"index.html.t"
		])
	}
	else {
		files.setIndexFiles([
			"index.html"
		])
	}
	server.pushRequestHandler(files)
	return true
}

main
{
	if new this().execute(args) == false:
		return 1
	return 0
}
