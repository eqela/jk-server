
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2021 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.fs
import jk.log
import jk.env
import jk.jwt
import jk.json
import jk.http.worker
import jk.http.server

class is HTTPWorkerRouter is HTTPServerComponent:

property jwtVerificationKey:jwtVerificationKeyValue as string
{
	get {
		if not jwtVerificationKeyValue:
			jwtVerificationKeyValue = EnvironmentVariable.get("WEBSITE_JWT_VERIFICATION_KEY")
		return jwtVerificationKeyValue
	}
}

func initializeWebsite virtual:
	nop

func initialize(server as HTTPServerBase):
	initializeWebsite()

func onMaintenance:
	nop

func onRefresh:
	nop

func cleanup:
	nop

func redirect(resp as HTTPWorkerResponse, url as string)
{
	resp.setStatus("303")
	resp.addHeader("Location", url)
	resp.setBody(String.toUTF8Buffer(url))
}

func sendHtml(resp as HTTPWorkerResponse, html as string)
{
	resp.setStatus("200")
	resp.setResponseHtml(html)
}

func handleAnonymousQuery(req as HTTPWorkerRequest, resp as HTTPWorkerResponse, handler as function)
{
	try {
		handler()
	}
	catch e {
		Log.error(getCtx(), String.asString(e))
		var status = "500"
		var r = new DynamicMap()
		r.setString("status", "error")
		if e is ExceptionWithError {
			var ee = e as! ExceptionWithError
			if ee.getErrorCode() == "notAuthenticated":
				status = "401"
			r.setString("errorCode", ee.getErrorCode())
			r.setString("errorDetail", String.asString(ee.getErrorDetail()))
		}
		resp.setStatus(status)
		resp.setBody(String.toUTF8Buffer(JSONEncoder.toString(r)))
	}
}

func validateToken(token as string) virtual as JWT
{
	if jwtVerificationKey {
		assert String.isNotEmpty(token)
		var cmap = new map
		Map.set(cmap, "rs256", jwtVerificationKey)
		var jwt = assert JWT.decode(token, cmap)
		assert jwt.isActive()
		return jwt
	}
	return null
}

func handleAuthenticatedQuery(req as HTTPWorkerRequest, resp as HTTPWorkerResponse, handler as function<void,JWT>)
{
	handleAnonymousQuery(req, resp, func {
		var auth = req.getHeaderValue("authorization")
		if String.isEmpty(auth):
			Error.throw("notAuthenticated", null)
		if String.startsWith(auth, "Bearer ") == false:
			Error.throw("notAuthenticated", null)
		var token = String.getEndOfString(auth, 7)
		var jwt = validateToken(token)
		if not jwt:
			Error.throw("notAuthenticated", null)
		handler(jwt)
	})
}
