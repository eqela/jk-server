
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2021 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.fs
import jk.env
import jk.log
import jk.server.web
import jk.http.server.fs

class:

property contentDirectory as File
{
}

property processTemplatesInContentDirectory as bool = true
{
}

func execute(args as array<string>, worker as typeinfo) as int
{
	var ws = new WebServer()
	var contentDir = contentDirectory
	if not contentDir {
		var envdir = EnvironmentVariable.get("WEBSITE_CONTENT_DIRECTORY")
		if String.isNotEmpty(envdir):
			contentDir = File.forPath(envdir)
	}
	if not contentDir {
		Log.debug(ws.getCtx(), "No WEBSITE_CONTENT_DIRECTORY defined.")
		var appdir = CommonPath.getAppDirectory()
		if appdir {
			contentDir = appdir.entry("wwwroot")
			if not contentDir.isDirectory() {
				Log.debug(ws.getCtx(), "No content directory exists here: `" .. String.asString(contentDir) .. "'")
				contentDir = null
			}
		}
		else {
			Log.warning(ws.getCtx(), "Application directory could not be determined.")
		}
	}
	if contentDir {
		if not contentDir.isDirectory():
			Error.throw("noSuchDirectory", contentDir)
		var handler = HTTPServerDirectoryHandler.forDirectory(contentDir)
		handler.setProcessTemplateFiles(processTemplatesInContentDirectory)
		if processTemplatesInContentDirectory {
			handler.setIndexFiles([
				"index.html",
				"index.html.t"
			])
		}
		ws.setHandler(handler)
		Log.info(ws.getCtx(), "Using content directory: `" .. String.asString(contentDir) .. "'")
	}
	else {
		Log.info(ws.getCtx(), "No content directory is set. Not serving any static content.")
	}
	ws.setWorker(worker)
	return ws.executeMain(args)
}
