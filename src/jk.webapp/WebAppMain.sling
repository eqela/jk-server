
/*
 * This file is part of Jkop
 * Copyright (c) 2016-2021 J42 Pte Ltd
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

import jk.fs
import jk.env
import jk.log
import jk.server.web
import jk.http.server
import jk.http.server.fs

class:

property contentDirectory as File
{
}

property processTemplatesInContentDirectory as bool = true
{
}

func pushAuthenticationHandler(ctx as LoggingContext, stack as HTTPServerRequestHandlerStack)
{
	var allowDummySession = Boolean.asBoolean(EnvironmentVariable.get("DUMMY_SESSION"))
	if allowDummySession {
		stack.pushRequestHandler(DummyAuthenticator.forEnvironment(ctx))
	}
	else if String.equalsIgnoreCase(EnvConfig.getString("WEBAPP_AUTHENTICATOR"), "OIDC") {
		var oidc = OidcAuthenticator.forEnvironment(ctx)
		if oidc:
			stack.pushRequestHandler(oidc)
	}
}

func pushContentDirectoryHandler(ctx as LoggingContext, stack as HTTPServerRequestHandlerStack)
{
	var contentDir = contentDirectory
	if not contentDir {
		var envdir = EnvironmentVariable.get("WEBAPP_CONTENT_DIRECTORY")
		if String.isNotEmpty(envdir):
			contentDir = File.forPath(envdir)
	}
	if not contentDir {
		Log.debug(ctx, "No WEBAPP_CONTENT_DIRECTORY defined.")
		var appdir = CommonPath.getAppDirectory()
		if appdir {
			contentDir = appdir.entry("static")
			if not contentDir.isDirectory() {
				Log.debug(ctx, "No content directory exists here: `" .. String.asString(contentDir) .. "'")
				contentDir = null
			}
		}
		else {
			Log.warning(ctx, "Application directory could not be determined.")
		}
	}
	if contentDir {
		if not contentDir.isDirectory():
			Error.throw("noSuchDirectory", contentDir)
		var handler = HTTPServerDirectoryHandler.forDirectory(contentDir)
		handler.setProcessTemplateFiles(processTemplatesInContentDirectory)
		if processTemplatesInContentDirectory {
			handler.setIndexFiles([
				"index.html",
				"index.html.t"
			])
		}
		stack.pushRequestHandler(handler)
		Log.info(ctx, "Using content directory: `" .. String.asString(contentDir) .. "'")
	}
	else {
		Log.info(ctx, "No content directory is set. Not serving any static content.")
	}
}

func execute(args as array<string>, worker as typeinfo) as int
{
	var ws = new WebServer()
	try {
		var stack = new HTTPServerRequestHandlerStack()
		pushAuthenticationHandler(ws.getCtx(), stack)
		pushContentDirectoryHandler(ws.getCtx(), stack)
		ws.setHandler(stack)
		ws.setWorker(worker)
		return ws.executeMain(args)
	}
	catch e {
		Log.error(ws.getCtx(), String.asString(e))
	}
	return 1
}
